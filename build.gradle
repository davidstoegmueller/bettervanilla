plugins {
    id 'java'
    id('io.papermc.paperweight.userdev') version "${dependency_papermc_paperweight_user_dev}"
    id 'com.gradleup.shadow' version '9.3.1'
}

group = 'com.daveestar'
version = plugin_version

repositories {
    mavenCentral()
    maven {
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:${dependency_papermc_api}"
    paperweight.paperDevBundle(dependency_papermc_paperweight)
    implementation 'org.bstats:bstats-bukkit:3.0.2'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

jar {
    archiveClassifier.set('plain')
    manifest {
        attributes(
            'Main-Class': 'com.daveestar.bettervanilla.Main'
        )
    }
}

shadowJar {
    archiveClassifier.set('')
    relocate 'org.bstats', 'com.daveestar.bettervanilla.libs.bstats'
    manifest {
        attributes(
            'Main-Class': 'com.daveestar.bettervanilla.Main'
        )
    }
}

paperweight.reobfArtifactConfiguration = io.papermc.paperweight.userdev.ReobfArtifactConfiguration.getMOJANG_PRODUCTION()

tasks.register('deploy', Copy) {
    dependsOn tasks.named('shadowJar')
    onlyIf { local_server_plugin_dir != null }
    from tasks.named('shadowJar').map { it.archiveFile }
    into local_server_plugin_dir
    doFirst {
        println "Copying plugin jar to ${local_server_plugin_dir}"
    }
}

tasks.named('build') {
    dependsOn tasks.named('deploy')
}
